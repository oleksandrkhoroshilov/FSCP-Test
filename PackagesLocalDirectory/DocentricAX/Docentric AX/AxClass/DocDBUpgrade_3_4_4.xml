<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocDBUpgrade_3_4_4</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The DocDBUpgrade_3_4_4 class contains the upgrade script for the database.
/// </summary>
[DocDBUpgrade]
public class DocDBUpgrade_3_4_4
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>upgradeTo3_4_4_0</Name>
				<Source><![CDATA[
    /// <summary>
    /// Upgrade database to version 3.4.4.0 procedure
    /// </summary>
    /// <param name = "_fromVersion">Upgrading from version</param>
    /// <param name = "_toVersion">Upgrade to versoin</param>
    /// <returns>True if upgrade was completed</returns>
    [DocDBUpgrade('3.4.4.0')]
    public static boolean upgradeTo3_4_4_0(DocDBVersion _fromVersion, DocDBVersion _toVersion)
    {
        str msg = '';

        ttsBegin;

        DocParameters docParameters = DocParameters::find();

        // Enable the License plate labels feature if the model DocentricAXWarehouseLabels has been installed
        if (docParameters && DocGlobalHelper::isModelInstalled(DocConstant::DocentricAXWarehouseLabelsModelName))
        {
            docParameters.selectForUpdate(true);
            docParameters.EnableWhsLicensePlateLabels = true;
            docParameters.update();
            msg += strFmt('The flag %1 in the table %2 was set to the value (%3).', fieldStr(DocParameters, EnableWhsLicensePlateLabels), tableStr(DocParameters), docParameters.EnableWhsLicensePlateLabels);
        }

        // Disable parent language fallback for user-defined labels in the DocSystemsSetting table if user-defined labels already exist
        DocLabelTranslation docLabelTranslation;
        select count(RecId) from docLabelTranslation;
        if (docLabelTranslation.RecId > 0)
        {
            DocSystemSetting docSystemSetting = DocSystemSetting::find(DocConstantSystemSetting::DisableParentLangFallbackForUDLabels);
            if (!docSystemSetting.RecId)
            {
                docSystemSetting.selectForUpdate(true);
                docSystemSetting.Name = DocConstantSystemSetting::DisableParentLangFallbackForUDLabels;
                docSystemSetting.Value = enum2Symbol(enumNum(NoYes), NoYes::Yes);
                docSystemSetting.insert();
                msg += strFmt('The flag %1 in the table %2 was set to the value (%3).', docSystemSetting.Name, tableStr(DocSystemSetting), docSystemSetting.Value);
            }
        }

        if (System.String::IsNullOrEmpty(msg))
        {
            msg = 'No change in the database.';
        }

        DocUpgradeLog::log(_fromVersion, _toVersion, Exception::Info, msg);

        ttsCommit;

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>