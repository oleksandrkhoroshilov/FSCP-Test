<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocSysTableLookup</Name>
	<SourceCode>
		<Declaration><![CDATA[
// Improved SysTableLookup class that allows custom manipulation with lookup form datasource
class DocSysTableLookup extends SysTableLookup
{
    FormRun formRun;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>performFormLookup</Name>
				<Source><![CDATA[
    void performFormLookup()
    {
        FormRealControl     callingRealControl;
        FormStringControl   callingStringControl;
        FormIntControl      callingIntControl;
        FormInt64Control    callingInt64Control;
        FormDateControl     callingDateControl;
        
        if (!callingControl)
        {
            throw(error(Error::wrongUseOfFunction(funcName())));
        }
        
        this.OnPerformingFormLookup(this.callingControl);
        
        formRun = this.formRun();

        switch (callingControl.handle())
        {
            case classNum(FormRealControl):
                callingRealControl = callingControl;
                callingRealControl.performFormLookup(formRun);
                break;
            case classNum(FormStringControl):
                callingStringControl = callingControl;                
                callingStringControl.performFormLookup(formRun);
                break;
            case classNum(FormIntControl):
                callingIntControl = callingControl;
                callingIntControl.performFormLookup(formRun);
                break;
            case classNum(FormInt64Control):
                callingInt64Control = callingControl;
                callingInt64Control.performFormLookup(formRun);
                break;
            case classNum(FormDateControl):
                callingDateControl = callingControl;
                callingDateControl.performFormLookup(formRun);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>datasource</Name>
				<Source><![CDATA[
    // Use this method to manipulate the datasource of the lookup form after the call to performFormLookup
    FormDataSource datasource()
    {
        return formRun.dataSource();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    static DocSysTableLookup construct()
    {
        return new DocSysTableLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newParameters</Name>
				<Source><![CDATA[
    static DocSysTableLookup newParameters(TableId tableId, FormControl callingControl, boolean useLookupValue = true, AmbiguousReferenceResolver _referenceResolver = null)
    {
        DocSysTableLookup  sysTableLookup = DocSysTableLookup::construct();
        ;
        sysTableLookup.parmTableId(tableId);
        sysTableLookup.parmCallingControl(callingControl);
        sysTableLookup.parmUseLookupValue(useLookupValue);
        if (_referenceResolver)
        {
            sysTableLookup.referenceResolver = _referenceResolver;
        }
        else if (callingControl != null)
        {
            sysTableLookup.referenceResolver = FormControlAmbiguousReferenceResolver::useExistingOrCreate(callingControl);
        }

        return sysTableLookup;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>