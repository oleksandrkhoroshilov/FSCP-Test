<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocumentFileHelper_DC_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class DocumentFileHelper_DC_Extension is an extension of the class DocumentFileHelper.
/// </summary>
[ExtensionOf(classStr(DocumentFileHelper))]
final class DocumentFileHelper_DC_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDocumentFromUser</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a document from the user, attaching the document to the parameter record, using the parameter
    /// document type.
    /// </summary>
    /// <param name="_record">The record to attach the document to.</param>
    /// <param name="_documentType">The document type to associated with the document from the user.</param>
    /// <param name="_documentUploadStyle">The document upload control's style that affects the input controls.</param>
    /// <returns>The file ID of the newly attached file.</returns>
    public static str getDocumentFromUser(
        Common _record,
        str _documentType,
        DocumentUploadStyle _documentUploadStyle)
    {
        str fileId =  next getDocumentFromUser(_record, _documentType, _documentUploadStyle);

        // When adding a Global attachment from the Global attachments form, set inserting docuRef.IsGlobalAttachment_DC to Yes.
        if ((_record.TableId == tableNum(DocuRef)) && fileId)
        {
            DocuRef         oldDocuRef = _record;
            FormDataSource  ds = FormDataUtil::getFormDataSource(oldDocuRef);
            FormRun         formRun = (ds ? ds.formRun() : null);
            FormRun         callerFormRun = (formRun && formRun.name() == formStr(DocuView) ? formRun.getLocalFormRun_DC() : null);

            if ( (callerFormRun && callerFormRun.name() == formStr(DocReportGlobalAttachment)) ||
                 (formRun && formRun.name() == formStr(DocReportAttachmentDetail)) )
            {
                ttsBegin;
            
                // Find the record that was added through the document upload mechanism
                DocuRef     docuRef;
                DocuValue   docuValue;

                select firstOnly forUpdate docuRef join docuValue where
                    docuRef.ValueRecId == docuValue.RecId &&
                    docuValue.FileId == guidFromString(fileId);

                if (docuRef.RecId)
                {
                    DocuType docuType = docuRef.docuType();

                    if (SysDictClass::isSuperclass(docuType.ActionClassId, classNum(DocuActionFile)))
                    {
                        docuRef.IsGlobalAttachment_DC = NoYes::Yes;
                        docuRef.update();
                    }
                }
            
                ttsCommit;
            }
        }

        return fileId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>