<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocTableBrowser</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Copy of SysTableBrowser class that allows editing Docentric parameters tables on non-development environments.
/// It seems that the SysTableBrowser form name is hard-coded in the kernel to be read-only on non-development environments.
/// </summary>
public class DocTableBrowser
{
    boolean     allowEdit;
    boolean     allowDelete;
    boolean     allowCreate;
    indexId     defaultIndexId;

    str         tableName;
    DictTable   dictTable;
    List        systemFieldControlIds;

    FormRun                         tableBrowserFormRun;
    FormBuildDesign                 formBuildDesign;
    FormBuildDataSource             formBuildDataSource;
    FormBuildGridControl            formBuildGridControl2;
    FormGridControl                 formGridControl;
    FormRadioControl                formRadioControl;    
    FormBuildGridControl            allfieldsGridControl;


    #DEFINE.FormWidth(500)
    #DEFINE.FormHeight(300)

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addField</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the specified field to the grid control.
    /// </summary>
    /// <param name="_tableId">
    /// The <c>tableId</c> value that the field is on.
    /// </param>
    /// <param name="_fieldId">
    /// The ID of the field.
    /// </param>
    /// <param name="_formBuildDataSource">
    /// The <c>FormBuildDataSource</c> on the form.
    /// </param>
    /// <param name="_formBuildGridControl">
    /// The grid control on the form.
    /// </param>
    /// <param name="_allowControlEdit">
    /// A Boolean value that specifies whether to allow editing of the control.
    /// </param>
    protected void addField(tableId _tableId,
        fieldId _fieldId,
        FormBuildDataSource _formBuildDataSource,
        FormBuildGridControl _formBuildGridControl,
        boolean _allowControlEdit)
    {
        FormBuildStringControl formBuildStringControl;
        FormBuildInt64Control formBuildInt64Control;
        FormBuildIntControl formBuildIntControl;
        FormBuildComboBoxControl formBuildComboBoxControl;
        FormBuildCheckBoxControl formBuildCheckBoxControl;
        FormBuildTimeControl formBuildTimeControl;
        FormBuildDateControl formBuildDateControl;
        FormBuildGuidControl formBuildGuidControl;
        FormBuildDateTimeControl formBuildDateTimeControl;
        FormBuildRealControl formBuildRealControl;
        FormBuildSegmentedEntryControl formBuildSegmentedEntryControl;
        FormBuildReferenceGroupControl formBuildReferenceGroupControl;

        FormBuildControl formBuildControl;
        SysDictField dictField;
        int j;
        tableId rootTableId;
        str controlName;
        UtilElementName utilElmName;
        Types baseType;

        str sqlFieldName()
        {
            return (select SqlName from SysTableFieldIdView where SysTableFieldIdView.Id == _tableId && SysTableFieldIdView.FieldId == _fieldId).SqlName;
        }
        ;

        dictField = new SysDictField(_tableId, _fieldId);
        if(dictField.isSystem())
        {
            //System fields are only stored on the root table, so get the information from there.
            rootTableId = SysDictTable::getRootTable(_tableId);
            dictField = new SysDictField(rootTableId, _fieldId);
        }

        if (dictField.saveContents())
        {
            for(j = 1;j <= dictField.arraySize(); j++)
            {
                formBuildControl = null;

                controlName = strfmt('%1_%2', dictField.name(), j);
                baseType = dictField.baseType();
                utilElmName = dictField.typeName();

                switch (baseType)
                {
                    case Types::String:
                    case Types::RString:
                    case Types::VarString:
                        formBuildStringControl = _formBuildGridControl.addDataField(_formBuildDataSource.id(), _fieldId, j);
                        formBuildStringControl.autoDeclaration(true);
                        formBuildStringControl.label(dictField.name());

                        formBuildStringControl.displayHeightMode(0);
                        formBuildStringControl.displayHeightValue(1);

                        formBuildControl = formBuildStringControl;
                        break;

                    case Types::Integer:

                        if (Global::isTypeTime(utilElmName))
                        {
                            formBuildTimeControl = _formBuildGridControl.addDataField(_formBuildDataSource.id(), _fieldId, j);
                            formBuildTimeControl.autoDeclaration(true);
                            formBuildTimeControl.label(dictField.name());

                            formBuildControl = formBuildTimeControl;
                        }
                        else
                        {
                            formBuildIntControl = _formBuildGridControl.addDataField(_formBuildDataSource.id(), _fieldId, j);
                            formBuildIntControl.autoDeclaration(true);
                            formBuildIntControl.label(dictField.name());

                            formBuildControl = formBuildIntControl;
                        }
                        break;

                    case Types::Int64:
                        // Reference Controls are created by default for surrogate foreign keys. However, the raw 64-bit
                        // integer should always be displayed. Force all Int64 type fields to use an Int64 Control.
                        formBuildControl = _formBuildGridControl.addDataField(_formBuildDataSource.id(), _fieldId, j);

                        formBuildInt64Control = formBuildControl as formBuildInt64Control;
                        if (formBuildInt64Control != null)
                        {
                            formBuildInt64Control.autoDeclaration(true);
                            formBuildInt64Control.label(dictField.name());
                            
                            formBuildControl = formBuildInt64Control;
                        }
                        else
                        {
                            formBuildReferenceGroupControl = formbuildControl as FormBuildReferenceGroupControl;
                            if (formBuildReferenceGroupControl != null)
                            {
                                formBuildReferenceGroupControl.autoDeclaration(true);
                                formBuildReferenceGroupControl.label(dictField.name());

                                formBuildControl = formBuildReferenceGroupControl;
                            }
                            else
                            {
                                formBuildSegmentedEntryControl = formBuildControl as formBuildSegmentedEntryControl;
                                if (formBuildSegmentedEntryControl != null)
                                {
                                    formBuildSegmentedEntryControl.autoDeclaration(true);
                                    formBuildSegmentedEntryControl.label(dictField.name());

                                    formBuildControl = formBuildSegmentedEntryControl;
                                }
                            }
                        }

                        break;

                    case Types::Enum:
                        formBuildControl = _formBuildGridControl.addDataField(_formBuildDataSource.id(), _fieldId, j);

                        formBuildComboBoxControl = formBuildControl as formBuildComboBoxControl;
                        if (formBuildComboBoxControl != null)
                        {
                            formBuildComboBoxControl.autoDeclaration(true);
                            formBuildComboBoxControl.label(dictField.name());

                            formBuildControl = formBuildComboBoxControl;
                        }
                        else
                        {
                            formBuildCheckBoxControl = formBuildControl as FormBuildCheckBoxControl;
                            if (formBuildCheckBoxControl != null)
                            {
                                formBuildCheckBoxControl.autoDeclaration(true);
                                formBuildCheckBoxControl.label(dictField.name());

                                formBuildControl = formBuildCheckBoxControl;
                            }
                        }

                        break;

                    case Types::Real:
                        formBuildRealControl = _formBuildGridControl.addDataField(_formBuildDataSource.id(), _fieldId, j);
                        formBuildRealControl.autoDeclaration(true);
                        formBuildRealControl.label(dictField.name());
                        
                        formBuildControl = formBuildRealControl;

                        break;

                    case Types::Guid:
                        formBuildGuidControl = _formBuildGridControl.addDataField(_formBuildDataSource.id(), _fieldId, j);
                        formBuildGuidControl.autoDeclaration(true);
                        formBuildGuidControl.label(dictField.name());

                        formBuildControl = formBuildGuidControl;

                        break;

                    case Types::Date:
                        formBuildDateControl = _formBuildGridControl.addDataField(_formBuildDataSource.id(), _fieldId, j);
                        formBuildDateControl.autoDeclaration(true);
                        formBuildDateControl.label(dictField.name());
                       
                        formBuildControl = formBuildDateControl;
                        
                        break;

                    case Types::UtcDateTime:
                        formBuildDateTimeControl = _formBuildGridControl.addDataField(_formBuildDataSource.id(), _fieldId, j);
                        formBuildDateTimeControl.autoDeclaration(true);
                        formBuildDateTimeControl.label(dictField.name());

                        formBuildControl = formBuildDateTimeControl;

                        break;
                }


                if (formbuildControl != null)
                {
                    formbuildControl.name(controlName);
                    formbuildControl.helpText(dictField.label() + strFmt(' (%1: %2, %3: %4) ', "@SYS28466", dictField.Id(), "@SYS76251", sqlFieldName()));

                    formBuildSegmentedEntryControl = formBuildControl as FormBuildSegmentedEntryControl;
                    if (formBuildSegmentedEntryControl != null)
                    {
                        // Segmented Entry controls should never be editable in the Table Browser.
                        // There is no way to specify a Controller class so the user would see errors.
                        formBuildSegmentedEntryControl.allowEdit(false);
                    }
                    else
                    {
                        formbuildControl.allowEdit(_allowControlEdit);
                    }

                    if (dictField.isSystem())
                    {
                        systemFieldControlIds.addEnd(formbuildControl.id());
                    }
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <see cref="DocTableBrowser"/> class for the specified table.
    /// </summary>
    /// <param name="_tableName">The name of the table to be browsed and potentially edited.</param>
    public void new(TableName _tableName)
    {
        tableName = _tableName;
        
        systemFieldControlIds = new List(Types::Integer);
        allowEdit   = isSystemAdministrator() && isDeveloper();         // standard allows editing anytable but only on devboxes and just for SysAdmin
        allowEdit   = allowEdit || this.allowEditDocentric();           // DOCENTRIC additionally allows editing some Docentric tables for users with sufficient privileges
        allowDelete = allowEdit;
        allowCreate = allowEdit;
        defaultIndexId = 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    //AOSRunMode::Client
    public void run(TableId _tableId)
    {
        Form                            tableBrowserForm;
        Args                            args;
        Args                            browserArgs;
        int                             idx;
        //boolean                         hasAutoReportGroup;
        //SqlDataDictionary               sqlDataDict = new SqlDataDictionary();
        //SqlDictionary                   sqlDict;
        //str                             sqlTableName;
        
        if (!_tableId)
        {
            // Throw an exception here would not help since it would not show to end users when the form has not loaded.
            this.showError(strfmt("@ApplicationFoundation:SysTableBrowser_TableNotSynced", this.parmTablename()));
            return;
        }

        try
        {
            /* Even if the _tableId != 0 there may be still error like:
                Table has not been fully sync'd.
                It is actually a View, not a table.
                Etc.
            */
            SysDictTable dictT = new SysDictTable(_tableId);

            // The table must be compiled to pass this check, otherwise it would fail later on when the form loads its data source for the same reason.
            Common table = dictT.makeRecord();

            select firstonly * from table;
        }
        catch
        {
            this.showError(strfmt("@ApplicationFoundation:SysTableBrowser_TableNotSynced", this.parmTablename()));
            return;
        }

        this.dictTable = new DictTable(_tableId);

        browserArgs = new Args();
        browserArgs.name(formstr(DocTableBrowser));
        tableBrowserFormRun = classfactory.formRunClass(browserArgs);
        tableBrowserForm = tableBrowserFormRun.form();

        if (!tableBrowserForm)
        {
            throw error("@ApplicationFoundation:SysTableBrowser_MissingForm");
        }

        //If the table is abstract, don't allow creates in DocTableBrowser.
        if(dictTable.isAbstract())
        {
            allowCreate = false;
        }
        
        formBuildDataSource = tableBrowserForm.dataSource(1);
        formBuildDataSource.table(dictTable.Id());
        formBuildDataSource.name(dictTable.name());
        formBuildDataSource.autoQuery(true);
        formBuildDataSource.linkType(0);
        formBuildDataSource.allowCreate(allowCreate);
        formBuildDataSource.allowDelete(allowDelete);
        formBuildDataSource.allowEdit(allowEdit);
        formBuildDataSource.validTimeStateAutoQuery(ValidTimeStateAutoQuery::DateRange);
        formBuildDataSource.validTimeStateUpdate(ValidTimeStateUpdate::Correction);
        
        if (defaultIndexId)
        {
            formBuildDataSource.index(defaultIndexId);
        }
        
        str sqlTableName = (select SqlName from SysTableIdView where SysTableIdView.Id == _tableId).SqlName;

        formBuildDesign = tableBrowserForm.design();
        formBuildDesign.caption("@SYS54191" + ': ' + dictTable.name() + strFmt(' (%1: %2, %3: %4)', "@SYS22596", dictTable.Id(), "@SYS76251", sqlTableName));
        formBuildDesign.widthMode(-1);
        formBuildDesign.widthValue(#FormWidth);
        formBuildDesign.heightMode(-1);
        formBuildDesign.heightValue(#FormHeight);
        
        allfieldsGridControl = formBuildDesign.control('allFieldsGrid');
        allfieldsGridControl.dataSource(formBuildDataSource.id());
        allfieldsGridControl.allowEdit(allowEdit);
        idx = allfieldsGridControl.id();
        
        this.showAllFields();
        
        /*
        formBuildGridControl2 = formBuildDesign.control('autoReportGrid');
        formBuildGridControl2.dataSource(dictTable.name());
        formBuildGridControl2.allowEdit(allowEdit);
        formBuildGridControl2.visible(false);

        hasAutoReportGroup = this.showAutoFields(dictTable.Id(), formBuildGridControl2, formBuildDataSource);
        */

        args = new Args();
        args.object(tableBrowserForm);
        tableBrowserFormRun = classfactory.formRunClass(args);

        tableBrowserFormRun.enableCountryRegion(false);
        tableBrowserFormRun.init();
        tableBrowserFormRun.parmDocTableBrowser(this);
        tableBrowserFormRun.run();

        /*
        formRadioControl = formRun.getRadioControl();
        formRadioControl.visible(hasAutoReportGroup);

        formGridControl = formRun.control(idx);
        formGridControl.setFocus();

        // This call is here as a hack fix (AXSE:4564 or AX6:18807) for performance issue around Table Browser
        // loading time, and should be removed once a proper fix is in place.
        formGridControl.autoSizeColumns(false);

        formRun.detach();

        */
    }

]]></Source>
			</Method>
			<Method>
				<Name>showError</Name>
				<Source><![CDATA[
    private void showError(str error)
    {        
        var args = new Args();
        args.name(formstr(DocTableBrowser));

        FormRun tableBrowserForm = classfactory.formRunClass(args);
        tableBrowserForm.parmError(error);
        tableBrowserForm.enableCountryRegion(false);
        tableBrowserForm.init();
        tableBrowserForm.run();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAllowCreate</Name>
				<Source><![CDATA[
    public void setAllowCreate(boolean newValue)
    {
        allowCreate = newValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAllowDelete</Name>
				<Source><![CDATA[
    public void setAllowDelete(boolean newValue)
    {
        allowDelete = newValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAllowEdit</Name>
				<Source><![CDATA[
    public void setAllowEdit(boolean newValue)
    {
        allowEdit = newValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDefaultIndexId</Name>
				<Source><![CDATA[
    public void setDefaultIndexId(indexId newValue)
    {
        defaultIndexId = newValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showAllFields</Name>
				<Source><![CDATA[
    //AOSRunMode::Client
    protected void showAllFields()
    {
        DictField                       dictField;
        fieldId                         fieldId;
        int                             i;
        int                             fieldCnt;
        Map                             userDefinedFields;
        List                            systemFields;
        MapEnumerator                   mapIt;
        ListEnumerator                  listIt;

        userDefinedFields = new Map(Types::String, Types::Integer);
        systemFields      = new List(Types::Integer);

        //Include all of the base table fields so we have "complete records".
        fieldCnt = dictTable.fieldCnt(TableScope::IncludeBaseTables);
        for (i = 1; i <= fieldCnt; i++)
        {
            fieldId = dictTable.fieldCnt2Id(i, TableScope::IncludeBaseTables);

            dictField = new dictField(dictTable.id(), fieldId);
            if (dictField)
            {
                if (dictField.isSystem())
                {
                    systemFields.addEnd(fieldId);
                }
                else
                {
                    userDefinedFields.insert(dictField.name(), fieldId);
                }
            }
        }

        mapIt = userDefinedFields.getEnumerator();
        while (mapIt.moveNext())
        {
            this.AddField(dictTable.id(), mapIt.currentValue(), formBuildDataSource, allfieldsGridControl, this.allowEdit);
        }

        listIt = systemFields.getEnumerator();
        while (listIt.moveNext())
        {
            this.AddField(dictTable.id(), listIt.current(), formBuildDataSource, allfieldsGridControl, this.allowEdit);
        }

    }

]]></Source>
			</Method>
			<Method>
				<Name>showSystemFields</Name>
				<Source><![CDATA[
    /*
    //AOSRunMode::Client
    protected boolean showAutoFields(tableId              tableId,
                                  FormBuildGridControl formBuildGridControl,
                                  FormBuildDataSource  formBuildDataSource)
    {
        Object                      formBuildControl;
        DictFieldGroup              dictFieldGroup;
        DictType                    dictType;
        fieldId                     fieldId;
        Name                        methodName;
        Types                       types;
        int                         i;
        DictMethod                  dictMethod;
        boolean                     hasAutoReportGroup;

        // Search up the inheritance hierarchy to find the first instance of the
        // AutoReport field group.
        dictFieldGroup = new DictFieldGroup(tableId, 'AutoReport', true);

        if (dictFieldGroup)
        {
            hasAutoReportGroup = true;

            for (i=1; i <= dictFieldGroup.numberOfFields(); i++)
            {
                methodName  = dictFieldGroup.methodName(dictFieldGroup.field(i));

                if (methodName)
                {
                    dictMethod  = new DictMethod(UtilElementType::TableInstanceMethod, tableId, methodName);
                    dictType    = new DictType(dictMethod.returnId());

                    if (dictType)
                    {
                        types = dictType.baseType();
                    }
                    else
                    {
                        types = dictMethod.returnType();
                    }

                    switch (types)
                    {
                        case Types::String:
                            formBuildControl = formBuildGridControl.addControl(FormControlType::String, methodName);
                            formBuildControl.displayHeightMode(0);
                            formBuildControl.displayHeightValue(1);
                            break;
                        case Types::UtcDateTime:
                            formBuildControl = formBuildGridControl.addControl(FormControlType::DateTime, methodName);
                            break;
                        case Types::Time:
                            formBuildControl = formBuildGridControl.addControl(FormControlType::Time, methodName);
                            break;
                        case Types::Date :
                            formBuildControl = formBuildGridControl.addControl(FormControlType::Date, methodName);
                            break;
                        case Types::Enum :
                            formBuildControl = formBuildGridControl.addControl(FormControlType::ComboBox, methodName);
                            break;
                        case Types::Integer :
                            formBuildControl = formBuildGridControl.addControl(FormControlType::Integer, methodName);
                            break;
                        case Types::Real :
                            formBuildControl = formBuildGridControl.addControl(FormControlType::Real, methodName);
                            break;
                        case Types::Int64 :
                            formBuildControl = formBuildGridControl.addControl(FormControlType::Int64, methodName);
                            break;
                        case Types::Guid :
                            formBuildControl = formBuildGridControl.addControl(FormControlType::Guid, methodName);
                            break;
                    }

                    if (formBuildControl)
                    {
                        formBuildControl.dataSource(formBuildDataSource.id());
                        formBuildControl.dataMethod(methodName);
                        formBuildControl.allowEdit(false);

                        if (dictType)
                        {
                            formBuildControl.label(dictType.name());
                            formBuildControl.helpText(dictType.label());
                        }
                        else
                        {
                            formBuildControl.label(methodName);
                        }
                    }
                }
                else
                {
                    fieldId   = dictFieldGroup.field(i);
                    this.AddField(tableId, fieldId, formBuildDataSource, formBuildGridControl, false);
                }
            }
        }

        return hasAutoReportGroup;
    }
*/
    /// <summary>
    /// Hides or shows the system fields.
    /// </summary>
    /// <param name = "_showSystemFields">A Boolean value indicating if the system fields should be hidden or shown.</param>
    public void showSystemFields(boolean _showSystemFields)
    {
        int controlId;
        FormControl formControl;
        ListEnumerator systemFieldControlIdsEnumerator = systemFieldControlIds.getEnumerator();

        while (systemFieldControlIdsEnumerator.moveNext())
        {
            controlId = systemFieldControlIdsEnumerator.current();

            if (controlId)
            {
                formControl = tableBrowserFormRun.control(controlId);
                if (formControl != null)
                {
                    formControl.visible(_showSystemFields);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTablename</Name>
				<Source><![CDATA[
    public TableName parmTablename(TableName _tableName = tableName)
    {
        tableName = _tableName;
        return tableName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>treeNode2TableId</Name>
				<Source><![CDATA[
    public static tableId treeNode2TableId(TreeNode treeNode)
    {
        #TreeNodeSysNodeType
        tableId tableId;
        QueryBuildDataSource    queryBuildDataSource;
        Form                    form;
        FormBuildDataSource     formBuildDataSource;
        TreeNode                treeNodeDatasource;
        int                     datasourceCnt, i;

        switch (treeNode.TreeNodeType().id())
        {
            case #NT_DBTABLE:
            case #NT_DBVIEW:
            case #NT_DBMAP:
            case #SysNodeTypeSystemTable:
            case #SysNodeTypeTableCollection:
                tableId = tablename2id(treeNode.treeNodeName());
                break;

            case #SysNodeTypeQueryDatasource:                 //Query DataSource
                queryBuildDataSource = treeNode;
                tableId = queryBuildDataSource.table();
                break;

            case #SysNodeTypeFormDatasource:
                treeNodeDatasource = treeNode;
                treenode = SysTreeNode::getRootElement(treenode);

                if (treeNode.utilElement().RecordType == UtilElementType::Form ||
                    treeNode.utilElement().RecordType == UtilElementType::WebForm ||
                    treeNode.utilElement().RecordType == UtilElementType::DataSet)
                {
                    form = treeNode;
                    datasourceCnt = form.dataSourceCount();
                    for (i = 1; i <= datasourceCnt; i++)
                    {
                        formBuildDataSource = form.dataSource(i);
                        if (formBuildDataSource.name() == treeNodeDatasource.treeNodeName())
                        {
                            tableId = formBuildDataSource.table();
                            break;
                        }
                    }
                }
                break;
        }
        return tableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// The entry point for running the DocTableBrowser.
    /// </summary>
    /// <param name="args">The Args object, expected to contain the 'TableName' query parameter for specifying the table to browse and edit.</param>
    /// <remarks>
    /// Table name is taken from 'TableName' URL parameter.
    /// </remarks>
    public static void main(Args args)
    {
        DocTableBrowser tableBrowser;
        URLUtility urlUtil = new URLUtility();

        str tName;

        tName = urlUtil.getQueryParamValue('TableName');        
        if (!tName)
        {
            throw error("@ApplicationFoundation:SysTableBrowser_MissingTableName");
        }

        tableBrowser = new DocTableBrowser(tName);

        if (!(tableBrowser.allowReadDocentric() || tableBrowser.allowEdit))
        {
            throw error(strfmt("@DocentricAX3:TableBrowsingNotSupported", tName));
        }

        tableBrowser.run(tablename2id(tName));
    }

]]></Source>
			</Method>
			<Method>
				<Name>allowReadDocentric</Name>
				<Source><![CDATA[
    /*****    DOCENTRIC CODE - BEGIN   *****/
    /// <summary>
    /// Determines whether the user is allowed to read the table.
    /// </summary>
    /// <returns>True if a user is allowed to read the table, otherwise False.</returns>
    boolean allowReadDocentric()
    {
        // Docentric tables allowed for reading, even if you are not an administrator
        container docentricReadTables = [tableStr(DocUpgradeLog), tableStr(DocTraceTable)];

        return isSystemAdministrator() || conFind(docentricReadTables, tableName);
    }

]]></Source>
			</Method>
			<Method>
				<Name>allowEditDocentric</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether the user is allowed to edit the table.
    /// </summary>
    /// <returns>True if a user is allowed to edit the table, otherwise False.</returns>
    boolean allowEditDocentric()
    {
        // allow editing of tables listed in docentricEditTables container for users with sufficient privileges
        container docentricEditTables = [tableStr(DocentricEngineSetting), tableStr(DocFeaturePreview), tableStr(DocTraceParam), tableStr(DocTraceTable), tableStr(DocSystemSetting)];

        // NOTE: if you are a administrator in development environment you can edit and delete any table (standard behavior)
        return conFind(docentricEditTables, tableName);
    }
    /*****    DOCENTRIC CODE - END   *****/

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>