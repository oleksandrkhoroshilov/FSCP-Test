<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocFormatStringLookup</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class DocFormatStringLookup
{
    private boolean showNumberCurrencyFormat;
    private boolean showDateTimeFormat;


}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmShowNumberCurrencyFormat</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets the flag indicating that number formats should be shown.
    /// </summary>
    /// <param name = "_showNumberCurrencyFormat">Show number formats</param>
    /// <returns>True/False</returns>
    public boolean parmShowNumberCurrencyFormat(boolean _showNumberCurrencyFormat = showNumberCurrencyFormat)
    {
        showNumberCurrencyFormat = _showNumberCurrencyFormat;
        return showNumberCurrencyFormat;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmShowDateTimeFormat</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets the flag indicating that datetime formats should be shown.
    /// </summary>
    /// <param name = "_showDateTimeFormat">Show datetime formats</param>
    /// <returns>True/False</returns>
    public boolean parmShowDateTimeFormat(boolean _showDateTimeFormat = showDateTimeFormat)
    {
        showDateTimeFormat = _showDateTimeFormat;
        return showDateTimeFormat;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the DocFormatString class.
    /// </summary>
    /// <param name = "_showNumberCurrencyFormat">Show number formats</param>
    /// <param name = "_showDateTimeFormat">Show datetime foramts</param>
    protected void new(boolean _showNumberCurrencyFormat, boolean _showDateTimeFormat)
    {
        showNumberCurrencyFormat = _showNumberCurrencyFormat;
        showDateTimeFormat = _showDateTimeFormat;
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupFormatString</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens the format string lookup with the specified subset of formats.
    /// </summary>
    /// <param name="_callerFormControl">The form control that is opening the format string lookup.</param>
    /// <returns>An DocFormatStringTmp record buffer that references the selected record.</returns>
    public DocFormatStringTmp lookupFormatString(FormControl _callerFormControl)
    {
        Form formatStringLookupForm = new Form(formStr(DocFormatStringLookup));
        FormStringControl stringControl = _callerFormControl as FormStringControl;

        Args args = new Args();
        args.name(formStr(DocFormatStringLookup));
        args.caller(_callerFormControl.formRun());

        args.parmObject(this);
       
        // Perform form lookup on the caller form control
        FormRun formRun = FormAutoLookupFactory::buildLookupFromCustomForm(stringControl, formatStringLookupForm, AbsoluteFieldBinding::construct(fieldStr(DocFormatStringTmp, FormatString), tableStr(DocFormatStringTmp)), args);
        stringControl.performFormLookup(formRun);

        DocFormatStringTmp selectedRecord = formRun.selectRecordModeSelectedRecord();

        return selectedRecord;
    }

]]></Source>
			</Method>
			<Method>
				<Name>neShowOnlyNumberCurrencyFormats</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the DocFormatStringLookup class that shows only number formats.
    /// </summary>
    /// <returns>The instance of the DocFormatStringLookup class</returns>
    public static DocFormatStringLookup neShowOnlyNumberCurrencyFormats()
    {
        DocFormatStringLookup docFormatStringLookup = new DocFormatStringLookup(true, false);
        return docFormatStringLookup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newShowOnlyDateTimeFormats</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the DocFormatStringLookup class that shows only datetime formats.
    /// </summary>
    /// <returns>The instance of the DocFormatStringLookup class</returns>
    public static DocFormatStringLookup newShowOnlyDateTimeFormats()
    {
        DocFormatStringLookup docFormatStringLookup = new DocFormatStringLookup(false, true);
        return docFormatStringLookup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newShowAllFormats</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the DocFormatStringLookup class that shows all formats.
    /// </summary>
    /// <returns>The instance of the DocFormatStringLookup class</returns>
    public static DocFormatStringLookup newShowAllFormats()
    {
        DocFormatStringLookup docFormatStringLookup = new DocFormatStringLookup(true, true);
        return docFormatStringLookup;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>