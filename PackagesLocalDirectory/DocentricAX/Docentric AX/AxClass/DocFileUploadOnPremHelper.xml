<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocFileUploadOnPremHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
using DocFS = Docentric.AX.Storage.File;

class DocFileUploadOnPremHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>GetFileFromUser</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get's the file from user.
    /// </summary>
    /// <param name = "_allowedFileExtensions">In the form of '.gif,.jpg,.jpeg,.png,.doc,.docx'</param>
    /// <returns>File content and file info</returns>
    public static DocFS.File GetFileFromUser(str _allowedFileExtensions = '')
    {
        FormRun visualForm;
        Args args;
        DocFS.File resultFile;
        Array items = new Array(Types::String);
        str fileName;

        try
        {
            items.value(1, _allowedFileExtensions);
            items.value(2, '');
            items.value(3, '');

            args = new args();
            args.name(formstr(DocentricFileUploadOnPrem));
            args.parmObject(items);
            
            visualForm = ClassFactory::formRunClassOnClient(args);
            visualForm.init();
            visualForm.run();
            visualForm.wait();

            visualForm.detach();

            if (items.value(2) != '' && items.value(3) != '')
            {
                fileName = items.value(2);
                str fileContentStr = items.value(3);
                System.Byte[] fileContentBytes = DocGlobalHelper::convertFromBase64String(fileContentStr);
                
                resultFile = DocFS.File::CreateNew(fileName);
                resultFile.Bytes = fileContentBytes;
            }
        }
        catch (Exception::CLRError)
        {
            if (fileName != '')
            {
                DocGlobalHelper::handleClrException('', strFmt("@DocentricAX:FileNotUploadedWithFilename", fileName), Exception::Warning);
            }
            else
            {
                DocGlobalHelper::handleClrException('', "@DocentricAX:FileNotUploaded", Exception::Warning);
            }
        }
        catch
        {
            if (fileName != '')
            {
                DocGlobalHelper::handleWarning(strFmt("@DocentricAX:FileNotUploadedWithFilename", fileName));
            }
            else
            {
                DocGlobalHelper::handleWarning("@DocentricAX:FileNotUploaded");
            }
        }
        
        return resultFile;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>