<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocReportAttachmentResolved</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Report attachment resolved based on the report attachment definition.
/// </summary>
class DocReportAttachmentResolved extends DocDocuAttachment
{
    DocReportAttachmentRule     attachmentRule;
    str                         displayName;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public DocValidationResult validate()
    {
        DocValidationResult validationResult = new DocValidationResult();

        boolean isValid = true;
        str validationMessage = '';

        // Check the resolved attachment filename (displayName).
        if (!displayName)
        {
            isValid = false;
            validationMessage += (validationMessage == '' ? '' : '; ') + "@DocentricAX3:ResolvedAdditionalAttachmentFilenameEmptyError";
        }
        else if (DocPlaceholderManager::findAnyPlaceholderCandidate(displayName))
        {
            isValid = false;
            validationMessage += (validationMessage == '' ? '' : '; ') + strFmt("@DocentricAX3:ResolvedAdditionalAttachmentFilenameContainsPlaceholdersError", displayName);
        }


        // Check the document type 
        DocuRef     docuRef;
        DocuType    docuType;

        select firstonly RecId from docuRef
                where docuRef.RecId == this.parmDocuRefRecId()
            join TypeId, ActionClassId from docuType
                where docuType.DataAreaId == docuRef.ActualCompanyId
                    && docuType.TypeId == docuRef.TypeId;
        if (!SysDictClass::isEqualOrSuperclass(docuType.ActionClassId, classNum(DocuActionFile)))
        {
            isValid = false;
            validationMessage += (validationMessage == '' ? '' : '; ') +
                                 strFmt("@DocentricAX2:InvalidReportAttachmentActionClass", docuType.TypeId, classId2Name(docuType.ActionClassId));
        }

        validationResult.parmIsValid(isValid);
        validationResult.parmMessage(validationMessage);
        
        return validationResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>toString</Name>
				<Source><![CDATA[
    public str toString()
    {
        str msg = '';

        msg += strFmt('(%1: %2)', "@DocentricAX3:ResolvedAdditionalAttachmentFullFilename", this.parmFullFilename());

        return msg;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmAttachmentRule</Name>
				<Source><![CDATA[
    public DocReportAttachmentRule parmAttachmentRule(DocReportAttachmentRule _attachmentRule = attachmentRule)
    {
        attachmentRule = _attachmentRule;
        return attachmentRule;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDisplayName</Name>
				<Source><![CDATA[
    public str parmDisplayName(str _displayName = displayName)
    {
        displayName = _displayName;
        return displayName;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>