<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocSysEmailCleanupUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The DocSysEmailCleanupUIBuilder class is a UI builder for batch email sending status clean-up service..
/// </summary>
public class DocSysEmailCleanupUIBuilder extends SysOperationAutomaticUIBuilder
{
    DialogField dfCreatedDateRangeType;
    DialogField dfCreatedDateTimFrom;
    DialogField dfCreatedDateTimeTo;
    DialogField dfCreatedDatePeriodCode;

    DialogField dfLatestStatusChangeDateRangeType;
    DialogField dfLatestStatusChangeDateTimeFrom;
    DialogField dfLatestStatusChangeDateTimeTo;
    DialogField dfLatestStatusChangeDatePeriodCode;
    

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the dialog.
    /// </summary>
    public void build()
    {
        super();

        Dialog dlg = this.dialog() as Dialog;
        if (dlg)
        {
            // Add a static text form control to the dialog to display instructions
            FormBuildControl fbc = dlg.formBuildDesign().controlNum(1);
            FormBuildStaticTextControl instructionStaticTextCtrl = dlg.formBuildDesign().addControl(FormControlType::StaticText, identifierStr(instructionStaticText), fbc, true);
            instructionStaticTextCtrl.text("@DocentricAX4:SysEmailCleanupInstruction");
            instructionStaticTextCtrl.visible(true);
            
            FormBuildGroupControl latestStatusChangeGroup = dlg.formBuildDesign().control(identifierStr(LatestStatusChangeGroup));
            latestStatusChangeGroup.breakable(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called after the build method.
    /// </summary>
    public void postBuild()
    {
        super();

        // Bind the contract fields with the dialog fields
        dfCreatedDateRangeType = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(DocSysEmailCleanupDataContract, parmCreatedDateRangeType));
        dfCreatedDateTimFrom = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(DocSysEmailCleanupDataContract, parmCreatedDateTimeFrom));
        dfCreatedDateTimeTo = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(DocSysEmailCleanupDataContract, parmCreatedDateTimeTo));
        dfCreatedDatePeriodCode = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(DocSysEmailCleanupDataContract, parmCreatedDatePeriodCode));

        dfLatestStatusChangeDateRangeType = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(DocSysEmailCleanupDataContract, parmLatestStatusChangeDateRangeType));
        dfLatestStatusChangeDateTimeFrom = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(DocSysEmailCleanupDataContract, parmLatestStatusChangeDateTimeFrom));
        dfLatestStatusChangeDateTimeTo = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(DocSysEmailCleanupDataContract, parmLatestStatusChangeDateTimeTo));
        dfLatestStatusChangeDatePeriodCode = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(DocSysEmailCleanupDataContract, parmLatestStatusChangeDatePeriodCode));
        
        // Subscribe to the dialog field events
        dfCreatedDateRangeType.registerOverrideMethod(
            methodStr(FormComboBoxControl, modified),
            methodStr(DocSysEmailCleanupUIBuilder, modifiedDateRangeType),
            this);

        dfLatestStatusChangeDateRangeType.registerOverrideMethod(
            methodStr(FormComboBoxControl, modified),
            methodStr(DocSysEmailCleanupUIBuilder, modifiedDateRangeType),
            this);
        
        // Set the dialog controls
        this.setControls();
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifiedDateRangeType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the value of the CreatedDateRangeType or LatestStatusChangeDateRangeType is modified.
    /// </summary>
    /// <param name = "_formControl">Form control</param>
    /// <returns>True/False</returns>
    public boolean modifiedDateRangeType(FormComboBoxControl _formControl)
    {
        this.setControls();

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setControls</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the dialog controls.
    /// </summary>
    private void setControls()
    {
        switch (dfCreatedDateRangeType.value())
        {
            case DocDateRangeType::Fixed:
                dfCreatedDateTimFrom.visible(true);
                dfCreatedDateTimeTo.visible(true);
                dfCreatedDatePeriodCode.visible(false);
                break;

            case DocDateRangeType::Relative:
                dfCreatedDateTimFrom.visible(false);
                dfCreatedDateTimeTo.visible(false);
                dfCreatedDatePeriodCode.visible(true);
                break;
        }

        switch (dfLatestStatusChangeDateRangeType.value())
        {
            case DocDateRangeType::Fixed:
                dfLatestStatusChangeDateTimeFrom.visible(true);
                dfLatestStatusChangeDateTimeTo.visible(true);
                dfLatestStatusChangeDatePeriodCode.visible(false);
                break;

            case DocDateRangeType::Relative:
                dfLatestStatusChangeDateTimeFrom.visible(false);
                dfLatestStatusChangeDateTimeTo.visible(false);
                dfLatestStatusChangeDatePeriodCode.visible(true);
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>