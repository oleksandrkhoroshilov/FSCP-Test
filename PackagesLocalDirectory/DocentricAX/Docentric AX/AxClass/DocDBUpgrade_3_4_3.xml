<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocDBUpgrade_3_4_3</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The DocDBUpgrade_3_4_3 class contains the upgrade script for the database.
/// </summary>
[DocDBUpgrade]
public class DocDBUpgrade_3_4_3
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>upgradeTo3_4_3_0</Name>
				<Source><![CDATA[
    /// <summary>
    /// Upgrade database to version 3.4.3.0 procedure
    /// </summary>
    /// <param name = "_fromVersion">Upgrading from version</param>
    /// <param name = "_toVersion">Upgrade to versoin</param>
    /// <returns>True if upgrade was completed</returns>
    [DocDBUpgrade('3.4.3.0')]
    static boolean upgradeTo3_4_3_0(DocDBVersion _fromVersion, DocDBVersion _toVersion)
    {
        str msg = '';

        DocEmailAccount docEmailAccount;
        ttsBegin;

        select count(RecId) from docEmailAccount;
        if (docEmailAccount.RecId > 0)
        {
            // Enable all existing Additional email accounts, populate the AccountUserName field if needed, and set the AuthenticationRequired flag.
            while select forUpdate docEmailAccount
            {
                // In the versions < 3.4.3: all accounts were enabled.
                docEmailAccount.IsEnabled = NoYes::Yes;

                // In the versions < 3.4.3: the UserName field was mandatory, while the AccountUserName and EncryptedPassword fields weren't.
                // If the AccountUserName field was empty, the UserName field was used as SMTP login account instead of AccountUserName.
                // However, we can figure out if SMTP authentication was required based on whether EncryptedPassword was populated or not.
                // From the 3.4.3 version on, AccountUserName is a mandatory field used as SMTP account if the authentication is required.
                if (docEmailAccount.AccountUserName == '' && docEmailAccount.getPassword())
                {
                    docEmailAccount.AccountUserName = docEmailAccount.UserName;
                }

                // In the versions < 3.4.3: if the EncryptedPassword field was set, SMTP authentication was required.
                if (docEmailAccount.getPassword())
                {
                    docEmailAccount.AuthenticationRequired = NoYes::Yes;
                }
                else
                {
                    docEmailAccount.AuthenticationRequired = NoYes::No;
                }

                docEmailAccount.update();
            }

            msg += strFmt('The fields (%1, %2) were set for all %3 records. All Additional email sender accounts are enabled and authentication is set as required for the accounts with configured credentials.', 
                fieldStr(DocEmailAccount, IsEnabled), fieldStr(DocEmailAccount, AuthenticationRequired), tableStr(DocEmailAccount));
        }

        if (System.String::IsNullOrEmpty(msg))
        {
            msg = 'No change in the database.';
        }

        DocUpgradeLog::log(_fromVersion, _toVersion, Exception::Info, msg);

        ttsCommit;

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>