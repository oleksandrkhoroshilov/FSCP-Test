<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocEncryptedFieldsCleanup</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Validates encrypted fields in Docentric tables and clears fields that cannot be decrypted. 
/// Typically after migration (database copy) between environments. 
/// </summary>
public class DocEncryptedFieldsCleanup
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>clearEncryptedFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates encrypted fields in Docentric tables and clears fields that cannot be decrypted.
    /// </summary>
    /// <returns>Container that contains a description of cleared fields for every processed table or empty string if the table was not changed.</returns>
    /// <remarks>Extend this method to include additional tables that use Global::editEncryptedField() and Global::editEncryptedStringField().</remarks>    
    public static container clearEncryptedFields()
    {
        container changedFields = conNull();

        changedFields += DocEmailAccount::clearEncryptedFields();
        changedFields += DocElectronicSignatureCertificate::clearEncryptedFields();
        changedFields += DocPdfEncryptionSettings::clearEncryptedFields();
        changedFields += DocSharePointSecuritySettings::clearEncryptedFields();

        changedFields += DocAzureStorageAccount::clearEncryptedFields();
        changedFields += DocParameters::clearEncryptedFields();

        return changedFields;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns cleanup message if some fields were cleared.
    /// </summary>
    /// <returns>Summary message for upgrade log</returns>   
    public static str run()
    {
        container changedFields = DocEncryptedFieldsCleanup::clearEncryptedFields();

        // remove empty strings
        container changedFields2 = conNull();
        int changedFieldsLen = conLen(changedFields);

        for (int i=1; i<=changedFieldsLen; i++)
        {
            str fieldsMsg = conPeek(changedFields, i);

            if (fieldsMsg)
            {
                changedFields2 += fieldsMsg;
            }
        }

        if (changedFields2)
        {
            str clearedMsg = '';
            int changedFieldsLen2 = conLen(changedFields2);

            for (int i=1; i<=changedFieldsLen2; i++)
            {
                clearedMsg +=  (clearedMsg ? (i<changedFieldsLen2 ? ', ' : ' and ') : '') + conPeek(changedFields2, i);
            }

            return strFmt('Cleared %1.',  clearedMsg);
        }

        return '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    // You can run cleanup manually if encyption problem appears when database is not copied
    public static void main(Args _args)
    {
        str msg = DocEncryptedFieldsCleanup::run();

        info(msg ? msg : 'All encrypted fields are valid.');
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>