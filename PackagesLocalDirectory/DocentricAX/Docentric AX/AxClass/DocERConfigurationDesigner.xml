<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocERConfigurationDesigner</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// This class opens the ERModelMappingDesigner form. It's invoked from the action in the message bar.
/// </summary>
public class DocERConfigurationDesigner
{
    private ERModelMappingTable erModelMappingTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs the class with the specified arguments.
    /// </summary>
    /// <param name = "_args">The specified arguments.</param>
    public static void main(Args _args)
    {
        if (!(_args.record() is ERModelMappingTable))
        {
            DocGlobalHelper::handleError(Error::missingFormActiveBuffer(formStr(ERModelMappingDesigner), tableStr(ERModelMappingTable)), true, funcName(), true);
        }

        DocERConfigurationDesigner docERConfiguratorDesigner = new DocERConfigurationDesigner();
        docERConfiguratorDesigner.setERModelMappingTable(_args.record());

        Args args = _args;
        args.caller(docERConfiguratorDesigner);

        // This call requires methods getSolutionVersion and getModelMappingVersionTable defined on this class.
        new MenuFunction(menuItemDisplayStr(ERModelMappingDesigner), MenuItemType::Display).run(args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setERModelMappingTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the model mapping record.
    /// </summary>
    /// <param name = "_erModelMappingTable">The model mapping record.</param>
    public void setERModelMappingTable(ERModelMappingTable _erModelMappingTable)
    {
        erModelMappingTable = _erModelMappingTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSolutionVersion</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get the latest version of <c>ERSolutionVersionTable</c> record for the selected <c>ERModelMapping</c> value.
    /// The returned latest version depends on RunDraft flag in <c>ERSolutionTable</c>:
    ///     - if RunDraft is False, the latest version in status Completed or Shared is returned
    ///     - if RunDraft is True, the latest version in status Draft is returned
    ///
    /// NOTE: This method is required to run ERModelMappingDesigner form.
    ///       The method signature is copied from the form ERModelMappingTable.
    /// </summary>
    /// <returns>The latest version of <c>ERSolutionVersionTable</c> record.</returns>
    public ERSolutionVersionTable getSolutionVersion()
    {
        ERSolutionVersionTable erSolutionVersionTable;

        if (erModelMappingTable)
        {
            ERSolutionTable erSolutionTable = erModelMappingTable.ERSolutionTable();
            if (erSolutionTable)
            {
                erSolutionVersionTable = erSolutionTable.getLatestVersion(!erSolutionTable.RunDraft);
            }
        }
        
        return erSolutionVersionTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getModelMappingVersionTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get the latest version of <c>ERModelMappingVersionTable</c> record for the selected <c>ERModelMapping</c> value.
    ///
    /// NOTE: This method is required to run ERModelMappingDesigner form.
    ///       The method signature is copied from the form ERModelMappingTable.
    /// </summary>
    /// <returns>The latest version of <c>ERModelMappingVersionTable</c> record.</returns>
    public ERModelMappingVersionTable getModelMappingVersionTable()
    {
        ERModelMappingVersionTable erModelMappingVersionTable;

        if (erModelMappingTable)
        {
            erModelMappingVersionTable = erModelMappingTable.getLatestVersion();
        }
        
        return erModelMappingVersionTable;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>