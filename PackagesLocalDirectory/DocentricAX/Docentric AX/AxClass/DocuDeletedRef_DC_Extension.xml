<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocuDeletedRef_DC_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class DocuDeletedRef_DC_Extension is an extension of the table DocuDeletedRef.
/// </summary>
[ExtensionOf(tableStr(DocuDeletedRef))]
public final class DocuDeletedRef_DC_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts a DocuDeletedRef based on a deleted DocuRef.
    /// </summary>
    /// <param name = "docuRef">Deleted DocuRef to make into a DocuDeletedRef</param>
    public void insert(DocuRef docuRef)
    {
        if (docuRef)
        {
            this.LanguageId_DC = docuRef.LanguageId_DC;
            this.CategoryId_DC = docuRef.CategoryId_DC;
            this.Tags_DC = docuRef.Tags_DC;
            this.ActiveFrom_DC = docuRef.ActiveFrom_DC;
            this.ActiveTo_DC = docuRef.ActiveTo_DC;
            this.IsGlobalAttachment_DC = docuRef.IsGlobalAttachment_DC;
        }
        next insert(docuRef);
    }

]]></Source>
			</Method>
			<Method>
				<Name>active_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the flag indicating whether the attachment is currently active.
    /// </summary>
    /// <returns>True if the attachment is active else false.</returns>
    [SysClientCacheDataMethodAttribute(true)]
    public display DocReportAttachmentActive active_DC()
    {
        date currentDate = DocGlobalHelper::getCurrentUsersCurrentDate();

        if (this.ActiveFrom_DC <= currentDate && (!this.ActiveTo_DC || this.ActiveTo_DC >= currentDate))
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>refTablename_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// A display method for reference table name.
    /// </summary>
    /// <returns>Reference table name</returns>
    [SysClientCacheDataMethodAttribute(true)]
    public display TableName refTablename_DC()
    {
        return tableId2pname(this.RefTableId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>refCommon_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds the record the DocuDeletedRef is attached to, if it exists.
    /// </summary>
    /// <returns>The record the DocuDeletedRef is attached to, if it exists.</returns>
    public Common refCommon_DC()
    {
        DictTable dictTable = new DictTable(this.RefTableId);
        Common common;

        if (!dictTable)
        {
            return common;
        }

        if (!xDataArea::exist(this.RefCompanyId))
        {
            return common;
        }

        changecompany(this.RefCompanyId)
        {
            common = dictTable.makeRecord();

            select firstonly common where common.RecId == this.RefRecId;
        }

        if (!common && this.RefCompanyId != this.ActualCompanyId && xDataArea::exist(this.ActualCompanyId))
        {
            changecompany(this.ActualCompanyId)
            {
                common = dictTable.makeRecord();

                select common where common.RecId == this.RefRecId;
            }
        }

        return common;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showGlobalAttachmentSettings_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the related table is a DirPartyTable whose type is set to LegalEntity.
    /// </summary>
    /// <returns>True if the DirPartyType is LegalEntity else false.</returns>
    public DocShowGlobalAttachmentSetting showGlobalAttachmentSettings_DC()
    {
        DocShowGlobalAttachmentSetting docShowGlobalAttachmentSetting = NoYes::No;

        if (this.RefTableId == tableNum(DirPartyTable))
        {
            DirPartyTable dirPartyTable = DirPartyTable::findRec(this.RefRecId);
            if (dirPartyTable && dirPartyTable.type() == DirPartyType::LegalEntity)
            {
                docShowGlobalAttachmentSetting = NoYes::Yes;
            }
        }

        return docShowGlobalAttachmentSetting;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>