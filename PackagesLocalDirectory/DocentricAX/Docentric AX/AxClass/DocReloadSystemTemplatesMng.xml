<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocReloadSystemTemplatesMng</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class DocReloadSystemTemplatesMng is used as manager for reloading system templates.
/// </summary>
public class DocReloadSystemTemplatesMng
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs the class with the specified arguments.
    /// </summary>
    /// <param name = "_args">The specified arguments.</param>
    public static void main(Args _args)
    {
        int updatedReportCount, insertedReportCount, invalidReportCount, validatedReportCount;
        int reloadedSystemTemplatesCount;
        DocReportTable firstInvalidReportRecord;
        FormRun callerFormRun;
        FormDataSource callingFormDataSource = null;
        Set reportSet;
        MultiSelectionHelper helper;
        DocReportTable docReportTable;

        // Obtain datasource
        if (_args && _args.caller())
        {
            callerFormRun = _args.caller() as FormRun;
            if (callerFormRun != null)
            {
                callingFormDataSource = callerFormRun.dataSource(1);
            }
        }

        // Check which menu item has been activated
        switch (_args.menuItemName())
        {
            case menuItemActionStr(DocReloadAllSystemTemplates):
                try
                {
                    // Business logic for Reload all system templates
                    [updatedReportCount, insertedReportCount, invalidReportCount, firstInvalidReportRecord] =
                        DocReportingSetupHelper::reloadSystemTemplatesAndReports();
                
                    validatedReportCount = updatedReportCount + insertedReportCount;

                    if (insertedReportCount > 0)
                    {
                        DocGlobalHelper::handleInfo(strFmt("@DocentricAX2:SystemTemplatesInsertedCount", insertedReportCount));
                    }
                    if (updatedReportCount > 0)
                    {
                        DocGlobalHelper::handleInfo(strFmt("@DocentricAX2:SystemTemplatesUpdatedCount", updatedReportCount));
                    }
                    if (updatedReportCount == 0 && insertedReportCount == 0)
                    {
                        DocGlobalHelper::handleWarning("@DocentricAX2:NoSystemTemplatesInAOT");
                    }
                }
                catch
                {
                    DocGlobalHelper::handleError("@DocentricAX2:ErrorReloadingSystemTemplates");
                }
                break;
        
            case menuItemActionStr(DocLoadNewReports):
                try
                {
                    // business logic for Load new reports
                    [updatedReportCount, insertedReportCount, invalidReportCount, firstInvalidReportRecord] = 
                        DocReportingSetupHelper::reloadSystemTemplatesAndReports(true);

                    validatedReportCount = updatedReportCount + insertedReportCount;

                    if (insertedReportCount > 0)
                    {
                        DocGlobalHelper::handleInfo(strFmt("@DocentricAX2:SystemTemplatesInsertedCount", insertedReportCount));
                    }
                    if (insertedReportCount == 0)
                    {
                        DocGlobalHelper::handleWarning("@DocentricAX2:NoNewReportsInAOT");
                    }
                }
                catch
                {
                    DocGlobalHelper::handleError("@DocentricAX2:ErrorReloadingSystemTemplates");
                }
                break;
        
            case menuItemActionStr(DocReloadAllSystemTemplatesForSelectedReports):
                reportSet = new Set(Types::String);
                helper = MultiSelectionHelper::construct();

                try
                {
                    // business logic for Reload all system templates for selected reports
                    helper.parmDatasource(callingFormDataSource);

                    // fill reportSet with ReportIds from selected records
                    docReportTable = helper.getFirst();
                    while (docReportTable.RecId != 0)
                    {
                        reportSet.add(docReportTable.ReportId);
                        docReportTable = helper.getNext();
                    }

                    [reloadedSystemTemplatesCount, invalidReportCount, firstInvalidReportRecord, validatedReportCount] =
                        DocReportingSetupHelper::reloadSystemTemplatesForReportsV2(reportSet, false, false);

                    if (reloadedSystemTemplatesCount > 0)
                    {
                        DocGlobalHelper::handleInfo(strFmt("@DocentricAX2:ReloadedAllSystemTemplatesForSelectedReportsCount", reloadedSystemTemplatesCount));
                    }
                    if (reloadedSystemTemplatesCount == 0)
                    {
                        DocGlobalHelper::handleWarning("@DocentricAX2:NoSystemTemplatesInAOT");
                    }
                }
                catch
                {
                    DocGlobalHelper::handleError("@DocentricAX2:ErrorReloadingSystemTemplates");
                }
                break;
        
            case menuItemActionStr(DocLoadNewSystemTemplatesForSelectedReports):
                reportSet = new Set(Types::String);
                helper = MultiSelectionHelper::construct();

                try
                {
                    // business logic for Load new system templates for selected reports
                    helper.parmDatasource(callingFormDataSource);

                    // Fill reportSet with ReportIds from selected records
                    docReportTable = helper.getFirst();
                    while (docReportTable.RecId != 0)
                    {
                        reportSet.add(docReportTable.ReportId);
                        docReportTable = helper.getNext();
                    }

                    [reloadedSystemTemplatesCount, invalidReportCount, firstInvalidReportRecord, validatedReportCount] =
                        DocReportingSetupHelper::reloadSystemTemplatesForReportsV2(reportSet, true, false);

                    if (reloadedSystemTemplatesCount > 0)
                    {
                        DocGlobalHelper::handleInfo(strFmt("@DocentricAX2:LoadedNewSystemTemplatesForSelectedReportsCount", reloadedSystemTemplatesCount));
                    }
                    if (reloadedSystemTemplatesCount == 0)
                    {
                        DocGlobalHelper::handleWarning("@DocentricAX2:NoNewSystemTemplatesInAOTForSelectedReports");
                    }
                }
                catch
                {
                    DocGlobalHelper::handleError("@DocentricAX2:ErrorReloadingSystemTemplates");
                }
                break;

            case menuItemActionStr(DocAddReportsForPrintMgmtDocumentType):
                helper = MultiSelectionHelper::construct();

                try
                {
                    // business logic for Add reports of the same Print management document type as selected reports
                    helper.parmDatasource(callingFormDataSource);

                    docReportTable = helper.getFirst();
                    while (docReportTable.RecId)
                    {
                        insertedReportCount += DocReportingSetupHelper::addReportsForPrintMgmtDocumentType(docReportTable);

                        docReportTable = helper.getNext();
                    }

                    if (insertedReportCount > 0)
                    {
                        DocGlobalHelper::handleInfo(strFmt("@DocentricAX2:AddedReportsCount", insertedReportCount));
                    }
                    if (insertedReportCount == 0)
                    {
                        DocGlobalHelper::handleWarning("@DocentricAX2:NoNewReportsFound");
                    }
                }
                catch
                {
                    DocGlobalHelper::handleError("@DocentricAX2:ErrorAddingReportForPrintMgmtDocumentType");
                }
                break;

            default:
                // Unsupported menu item
                DocGlobalHelper::handleWarning("@DocentricAX2:UnsupportedAction");
                return;
        }

        if (callingFormDataSource != null)
        {
            // Refresh datasource
            callingFormDataSource.research(true);
            
            if (invalidReportCount > 0)
            {
                // Warn user of any invalid reports.
                if (invalidReportCount == 1 && validatedReportCount == 1)
                {
                    Box::warning(strFmt("@DocentricAX4:SingleInvalidReportsWarning", firstInvalidReportRecord.ReportId));
                }
                else
                {
                    Box::warning(strFmt("@DocentricAX4:MultipleInvalidReportsWarning", invalidReportCount, validatedReportCount));
                }

                // Select the first invalid report.
                callingFormDataSource.positionToRecord(firstInvalidReportRecord);

                // Open up the details view of the first invalid report.
                callerFormRun.viewOptionHelper().setViewOption(FormViewOption::Details);

                // Open up the General tab, so that user can see the 'Is report valid' control
                FormTabPageControl general = callerFormRun.control(callerFormRun.controlId(formControlStr(DocReportTable, General)));
                general.activatePage();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>