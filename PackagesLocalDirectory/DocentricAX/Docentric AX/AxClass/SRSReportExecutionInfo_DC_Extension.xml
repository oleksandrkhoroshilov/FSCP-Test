<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SRSReportExecutionInfo_DC_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class SRSReportExecutionInfo_DC_Extension is an extension of the class SRSReportExecutionInfo.
/// </summary>
[ExtensionOf(classStr(SRSReportExecutionInfo))]
final class SRSReportExecutionInfo_DC_Extension
{
    private const int DocentricPackVersion1 = DocConstant::DocentricBasePackVersion + 1;
    private DocReportExecutionInfo reportExecutionInfo_DC;
    

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmReportExecutionInfo_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets an instance of the DocReportExecutionInfo class.
    /// </summary>
    /// <param name = "_reportExecutionInfo_DC">An instance of the DocReportExecutionInfo class</param>
    /// <returns>An instance of the DocReportExecutionInfo class</returns>
    public DocReportExecutionInfo parmReportExecutionInfo_DC(DocReportExecutionInfo _reportExecutionInfo_DC = reportExecutionInfo_DC)
    {
        reportExecutionInfo_DC = _reportExecutionInfo_DC;

        if (reportExecutionInfo_DC == null)
        {
            reportExecutionInfo_DC = new DocReportExecutionInfo();
        }

        return reportExecutionInfo_DC;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Packs the class variables into the container.
    /// </summary>
    /// <returns>Packed container with class variables</returns>
    public container pack()
    {
        container packedClass = next pack();

        container packedClassExt = [DocentricPackVersion1,
                                        this.parmReportExecutionInfo_DC().pack()];

        return SysPackExtensions::appendExtension(packedClass, classStr(SRSReportExecutionInfo_DC_Extension), packedClassExt);
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Unpacks the class variables and re-instantiates them.
    /// </summary>
    /// <param name = "_packedClass">Packed container with class variables</param>
    /// <returns>True/False</returns>
    public boolean unpack(container _packedClass)
    {
        boolean result = next unpack(_packedClass);
        if (result)
        {
            container packedClassExt = SysPackExtensions::findExtension(_packedClass, classStr(SRSReportExecutionInfo_DC_Extension));

            Integer version = RunBase::getVersion(packedClassExt);
            container packedDocReportExecutionInfo_DC;

            switch (version)
            {
                case DocentricPackVersion1:
                    [version, packedDocReportExecutionInfo_DC] = packedClassExt;

                    this.parmReportExecutionInfo_DC().unpack(packedDocReportExecutionInfo_DC);

                    result = true;
                    break;

                default:
                    result = false;
            }
        }

        return result;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>