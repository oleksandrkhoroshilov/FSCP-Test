<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SrsPrintDestinationSettings_DC_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class SrsPrintDestinationSettings_DC_Extension is an extension of class SrsPrintDestinationSettings.
/// </summary>
[ExtensionOf(classStr(SrsPrintDestinationSettings))]
final class SrsPrintDestinationSettings_DC_Extension
{
    private const int DocentricPackVersion1 = DocConstant::DocentricBasePackVersion + 1;
    private const int DocentricPackVersion2 = DocConstant::DocentricBasePackVersion + 2;

    /* Additional members */
    private DocPrintDestSettingsGenerateDS  generateDSPrintDestSettings_DC;
    private DocPrintDestSettingsEmail       emailPrintDestSettings_DC;
    private DocPrintDestSettingsFile        filePrintDestSettings_DC;
    private DocPrintDestSettingsPrinter     printerPrintDestSettings_DC;
    private DocPrintDestSettingsExtTmp      tmpPrintDestSettings_DC;
    private DocSrsPrintReportSettings       srsPrintReportSettings_DC;
    private DocPrintDestSettingsMemory      memoryPrintDestSettings_DC;


}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmGenerateDSPrintDestSettings_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets an instance of the DocPrintDestSettingsGenerateDS class.
    /// </summary>
    /// <param name = "_generateDSPrintDestSettings_DC">An instance of the DocPrintDestSettingsGenerateDS class</param>
    /// <returns>An instance of the DocPrintDestSettingsGenerateDS class</returns>
    public DocPrintDestSettingsGenerateDS parmGenerateDSPrintDestSettings_DC(DocPrintDestSettingsGenerateDS _generateDSPrintDestSettings_DC = generateDSPrintDestSettings_DC)
    {
        generateDSPrintDestSettings_DC = _generateDSPrintDestSettings_DC;

        if (generateDSPrintDestSettings_DC == null)
        {
            generateDSPrintDestSettings_DC = new DocPrintDestSettingsGenerateDS();
        }

        return generateDSPrintDestSettings_DC;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmEmailPrintDestSettings_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets an instance of the DocPrintDestSettingsEmail class.
    /// </summary>
    /// <param name = "_emailPrintDestSettings_DC">An instance of the DocPrintDestSettingsEmail class</param>
    /// <returns>An instance of the DocPrintDestSettingsEmail class</returns>
    public DocPrintDestSettingsEmail parmEmailPrintDestSettings_DC(DocPrintDestSettingsEmail _emailPrintDestSettings_DC = emailPrintDestSettings_DC)
    {
        emailPrintDestSettings_DC = _emailPrintDestSettings_DC;

        if (emailPrintDestSettings_DC == null)
        {
            emailPrintDestSettings_DC = new DocPrintDestSettingsEmail();
        }

        return emailPrintDestSettings_DC;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFilePrintDestSettings_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets an instance of the DocPrintDestSettingsFile class.
    /// </summary>
    /// <param name = "_filePrintDestSettings_DC">An instance of the DocPrintDestSettingsFile class</param>
    /// <returns>An instance of the DocPrintDestSettingsFile class</returns>
    public DocPrintDestSettingsFile parmFilePrintDestSettings_DC(DocPrintDestSettingsFile _filePrintDestSettings_DC = filePrintDestSettings_DC)
    {
        filePrintDestSettings_DC = _filePrintDestSettings_DC;

        if (filePrintDestSettings_DC == null)
        {
            filePrintDestSettings_DC = new DocPrintDestSettingsFile();
        }

        return filePrintDestSettings_DC;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPrinterPrintDestSettings_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets an instance of the DocPrintDestSettingsPrinter class.
    /// </summary>
    /// <param name = "_printerPrintDestSettings_DC">An instance of the DocPrintDestSettingsPrinter class</param>
    /// <returns>An instance of the DocPrintDestSettingsPrinter class</returns>
    public DocPrintDestSettingsPrinter parmPrinterPrintDestSettings_DC(DocPrintDestSettingsPrinter _printerPrintDestSettings_DC = printerPrintDestSettings_DC)
    {
        printerPrintDestSettings_DC = _printerPrintDestSettings_DC;

        if (printerPrintDestSettings_DC == null)
        {
            printerPrintDestSettings_DC = new DocPrintDestSettingsPrinter();
        }

        return printerPrintDestSettings_DC;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmMemoryPrintDestSettings_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets an instance of the DocPrintDestSettingsMemory class.
    /// </summary>
    /// <param name = "_memoryPrintDestSettings_DC">An instance of the DocPrintDestSettingsMemory class</param>
    /// <returns>An instance of the DocPrintDestSettingsMemory class</returns>
    public DocPrintDestSettingsMemory parmMemoryPrintDestSettings_DC(DocPrintDestSettingsMemory _memoryPrintDestSettings_DC = memoryPrintDestSettings_DC)
    {
        memoryPrintDestSettings_DC = _memoryPrintDestSettings_DC;

        if (memoryPrintDestSettings_DC == null)
        {
            memoryPrintDestSettings_DC = new DocPrintDestSettingsMemory();
        }

        return memoryPrintDestSettings_DC;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTmpPrintDestSettings_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets an instance of the DocPrintDestSettingsExtTmp class.
    /// </summary>
    /// <param name = "_tmpPrintDestSettings_DC">An instance of the DocPrintDestSettingsExtTmp class</param>
    /// <returns>An instance of the DocPrintDestSettingsExtTmp class</returns>
    public DocPrintDestSettingsExtTmp parmTmpPrintDestSettings_DC(DocPrintDestSettingsExtTmp _tmpPrintDestSettings_DC = tmpPrintDestSettings_DC)
    {
        tmpPrintDestSettings_DC = _tmpPrintDestSettings_DC;

        if (tmpPrintDestSettings_DC == null)
        {
            tmpPrintDestSettings_DC = new DocPrintDestSettingsExtTmp();
        }

        return tmpPrintDestSettings_DC;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSrsPrintReportSettings_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets an instance of the DocSrsPrintReportSettings class.
    /// </summary>
    /// <param name = "_srsPrintReportSettings_DC">An instance of the DocSrsPrintReportSettings class</param>
    /// <returns>An instance of the DocSrsPrintReportSettings class</returns>
    public DocSrsPrintReportSettings parmSrsPrintReportSettings_DC(DocSrsPrintReportSettings _srsPrintReportSettings_DC = srsPrintReportSettings_DC)
    {
        srsPrintReportSettings_DC = _srsPrintReportSettings_DC;

        if (srsPrintReportSettings_DC == null)
        {
            srsPrintReportSettings_DC = new DocSrsPrintReportSettings();
        }

        return srsPrintReportSettings_DC;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Packs the class variables into the container.
    /// </summary>
    /// <returns>Packed container with class variables</returns>
    public container pack()
    {
        container packedClass = next pack();

        return SysPackExtensions::appendExtension(packedClass, classStr(SrsPrintDestinationSettings_DC_Extension), this.packInternal());
    }

]]></Source>
			</Method>
			<Method>
				<Name>packInternal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Packs the class variables into the container.
    /// </summary>
    /// <returns>Packed container with class variables</returns>
    private container packInternal()
    {
        return [DocentricPackVersion2,
                this.parmGenerateDSPrintDestSettings_DC().pack(),
                this.parmPrinterPrintDestSettings_DC().pack(),
                this.parmEmailPrintDestSettings_DC().pack(),
                this.parmFilePrintDestSettings_DC().pack(),
                this.parmTmpPrintDestSettings_DC().pack(),
                this.parmSrsPrintReportSettings_DC().pack(),
                this.parmMemoryPrintDestSettings_DC().pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Unpacks the class variables and re-instantiates them.
    /// </summary>
    /// <param name = "_packedClass">Packed container with class variables</param>
    /// <returns>True/False</returns>
    public boolean unpack(container _packedClass)
    {
        boolean result;
        boolean newPackVersion;

        Integer version = RunBase::getVersion(_packedClass);
        container packedClass;

        switch (version)
        {
            case DocentricPackVersion1:
                [result, packedClass] = this.unpackInternal(_packedClass);
                break;

            default:
                container packedClassFound = this.findPackedContainer(_packedClass);
                if (packedClassFound != conNull())
                {
                    [result, packedClass] = this.unpackInternal(packedClassFound);
                    break;
                }

                newPackVersion = true;
                packedClass = _packedClass;
                result = false;
        }

        result = next unpack(packedClass) && (newPackVersion || result);
        if (newPackVersion && result)
        {
            container packedClassInternal = SysPackExtensions::findExtension(packedClass, classStr(SrsPrintDestinationSettings_DC_Extension));
            if (!conPeek(this.unpackInternal(packedClassInternal), 1))
            {
                result = false;
            }
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findPackedContainer</Name>
				<Source><![CDATA[
    /// <summary>
    /// Recursively searches the packed container for the packed version DocentricPackVersion1.
    /// </summary>
    /// <param name = "_item">Container item</param>
    /// <returns>Packed container with the DocentricPackVersion1 version or empty container</returns>
    private container findPackedContainer(anytype _item)
    {
        if (typeOf(_item) == Types::Container)
        {
            int conLen = conLen(_item);
            for (int i = 1; i <= conLen; i++)
            {
                if (i == 1)
                {
                    Integer version = RunBase::getVersion(_item);
                    switch (version)
                    {
                        case 0:
                            return conNull();

                        case DocentricPackVersion1:
                            // Check the number of items in the packed container to be sure it belongs to this extension
                            if (conLen != 9)
                            {
                                break;
                            }
                            return _item;
                    }
                    continue;
                }

                container item = this.findPackedContainer(conPeek(_item, i));
                if (item != conNull())
                {
                    return item;
                }
            }
        }
        return conNull();
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpackInternal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Unpacks the class variables and re-instantiates them.
    /// </summary>
    /// <param name = "_packedClass">Packed container with class variables</param>
    /// <returns>Container with result information</returns>
    private container unpackInternal(container _packedClass)
    {
        container result = [false, conNull()];

        Integer version = RunBase::getVersion(_packedClass);
        container packedGenerateDSPrintDestSettings_DC;
        container packedPrinterPrintDestSettings_DC;
        container packedEmailPrintDestSettings_DC;
        container packedFilePrintDestSettings_DC;
        container packedTmpPrintDestSettings_DC;
        container packedSrsPrintReportSettings_DC;
        container packedMemoryPrintDestSettings_DC;
        container packedClass;

        void unpackItems()
        {
            this.parmGenerateDSPrintDestSettings_DC().unpack(packedGenerateDSPrintDestSettings_DC);
            this.parmPrinterPrintDestSettings_DC().unpack(packedPrinterPrintDestSettings_DC);
            this.parmEmailPrintDestSettings_DC().unpack(packedEmailPrintDestSettings_DC);
            this.parmFilePrintDestSettings_DC().unpack(packedFilePrintDestSettings_DC);
            this.parmTmpPrintDestSettings_DC().unpack(packedTmpPrintDestSettings_DC);
            this.parmSrsPrintReportSettings_DC().unpack(packedSrsPrintReportSettings_DC);
            this.parmMemoryPrintDestSettings_DC().unpack(packedMemoryPrintDestSettings_DC);
        }

        switch (version)
        {
            case DocentricPackVersion1:
                [version,
                    packedGenerateDSPrintDestSettings_DC,
                    packedPrinterPrintDestSettings_DC,
                    packedEmailPrintDestSettings_DC,
                    packedFilePrintDestSettings_DC,
                    packedTmpPrintDestSettings_DC,
                    packedSrsPrintReportSettings_DC,
                    packedMemoryPrintDestSettings_DC,
                    packedClass
                ] = _packedClass;

                unpackItems();

                result = [true, packedClass];
                break;

            case DocentricPackVersion2:
                [version,
                    packedGenerateDSPrintDestSettings_DC,
                    packedPrinterPrintDestSettings_DC,
                    packedEmailPrintDestSettings_DC,
                    packedFilePrintDestSettings_DC,
                    packedTmpPrintDestSettings_DC,
                    packedSrsPrintReportSettings_DC,
                    packedMemoryPrintDestSettings_DC
                ] = _packedClass;

                unpackItems();

                result = [true, conNull()];
                break;

            default:
                result = [false, conNull()];
        }
        return result;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>