<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DocuAttachment_DC_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class DocuAttachment_DC_Extension is an extension of the form DocuAttachment.
/// </summary>
[ExtensionOf(formStr(DocuAttachment))]
final class DocuAttachment_DC_Extension
{
    private FormRun localFormRun_DC;
    private boolean showGlobalAttachmentSetting_DC = false;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the form.
    /// </summary>
    public void init()
    {
        next init();

        // Get the caller form
        ObjectIdent lastActivatedForm = infolog.parmLastActivatedForm();
        if (lastActivatedForm)
        {
            localFormRun_DC = lastActivatedForm.object();
        }

        // Allow setting Global flag only if the caller form is legal entity or our form with global attachments.
        showGlobalAttachmentSetting_DC = (localFormRun_DC && (localFormRun_DC.name() == formStr(OMLegalEntity) || localFormRun_DC.name() == formStr(DocReportGlobalAttachment))) ||
                                            (this.args().parmEnumType() == enumNum(DocShowGlobalAttachmentSetting) && this.args().parmEnum());
        IsGlobalAttachment_DC.visible(showGlobalAttachmentSetting_DC);
        DocuRef_ds.object(fieldNum(DocuRef, IsGlobalAttachment_DC)).visible(showGlobalAttachmentSetting_DC);
    }

]]></Source>
			</Method>
			<Method>
				<Name>close</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the form is closed.
    /// </summary>
    public void close()
    {
        #Task

        next close();
        
        if (localFormRun_DC.name() == formStr(DocReportGlobalAttachment))
        {
            localFormRun_DC.task(#taskRefresh);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DocuRef_OnActivated_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the DocuRef table buffer becomes active.
    /// </summary>
    public void DocuRef_OnActivated_DC()
    {
        DocentricSettings_DC.enabled(SysDictClass::isSuperclass(DocuRef.docuType().ActionClassId, classNum(DocuActionFile)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>DocReportAttachmentDetail_OnClicked_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the DocReportAttachmentDetail menu button is clicked.
    /// </summary>
    public void DocReportAttachmentDetail_OnClicked_DC()
    {
        CompanyId actualCompanyId_DC = this.queryRefCompanyId;
        TableId actualTableId_DC = this.actualTableId();
        RecId actualRecId_DC = this.actualRecId();
        
        DictTable dictTable = new DictTable(actualTableId_DC);
        if (dictTable == null)
        {
            return;
        }

        Common testCursor = dictTable.makeRecord();

        changeCompany(actualCompanyId_DC)
        {
            select firstOnly testCursor where testCursor.RecId == actualRecId_DC;
        }
        
        if (!testCursor)
        {
            return;
        }

        Args args = new Args(formStr(DocReportAttachmentDetail));
        args.record(testCursor);
        args.lookupRecord(DocuRef);
        args.caller(this);
        args.parmEnum(showGlobalAttachmentSetting_DC);
        args.parmEnumType(enumNum(DocShowGlobalAttachmentSetting));

        // Open form instead of menu item to avoid security check. This button can always be clicked from DocuAttachment form.
        FormRun formRun = classFactory.formRunClass(args);
        formRun.init();
        formRun.run();
        formRun.wait();
        formRun.detach();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>