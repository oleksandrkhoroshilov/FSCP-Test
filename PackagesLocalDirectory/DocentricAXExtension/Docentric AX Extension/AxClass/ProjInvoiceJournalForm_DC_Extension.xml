<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjInvoiceJournalForm_DC_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class ProjInvoiceJournalForm_DC_Extension is an extension of the form ProjInvoiceJournal.
/// </summary>
[ExtensionOf(formStr(ProjInvoiceJournal))]
final class ProjInvoiceJournalForm_DC_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the form.
    /// </summary>
    public void init()
    {
        next init();

        // Check to see if the design exists, this is needed if the ProjInvoiceJournalV2 form is called instead, 
        // which will result in the design being null.
        if (!this.design())
        {
            return;
        }

        PrintProjInvoicePrintMgmtPreview_DC.registerOverrideMethod(methodStr(FormMenuButtonControl, clicked), formMethodStr(ProjInvoiceJournal, PrintProjInvoicePrintMgmtPreview_DC_Clicked_DC), this);
        PrintArchiveDownload_DC.registerOverrideMethod(methodStr(FormMenuButtonControl, clicked), formMethodStr(ProjInvoiceJournal, PrintArchiveDownload_DC_Clicked_DC), this);
        PrintArchive_DC.registerOverrideMethod(methodStr(FormMenuButtonControl, clicked), formMethodStr(ProjInvoiceJournal, PrintArchive_DC_Clicked_DC), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>PrintProjInvoicePrintMgmtPreview_DC_Clicked_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the PrintProjInvoicePrintMgmtPreview_DC menu item is clicked.
    /// </summary>
    /// <param name = "_control">A form control</param>
    public void PrintProjInvoicePrintMgmtPreview_DC_Clicked_DC(FormControl _control)
    {
        this.launchReport_DC(menuItemOutputStr(ProjInvoiceOriginalPrint));
    }

]]></Source>
			</Method>
			<Method>
				<Name>launchReport_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copied from ProjInvoiceJournal.
    /// Everything is same, we only added 2 lines where we set reportArgs.parm().
    /// </summary>
    /// <param name = "_menuItemName">Caller menu item name, will be used
    /// for explicitely executing it.</param>
    void launchReport_DC(MenuItemName _menuItemName)
    {
        ProjInvoiceJour       cursor;
        RecordSortedList      journalList;
        PrintMgmtDocumentType documentType;
        Map                   documentTypeMap = new Map(Types::Enum, Types::Class);
        MapEnumerator         docTypeMapEnum;
        Args                  controllerArgs;

        for (cursor = getFirstSelection(projInvoiceJour_ds);
        cursor;
        cursor = projInvoiceJour_ds.getNext())
        {
            documentType = PSAProjAndContractInvoiceController::getDocumentType(_menuItemName, cursor);

            if (documentTypeMap.exists(documentType))
            {
                journalList = documentTypeMap.lookup(documentType);
            }
            else
            {
                journalList = new RecordSortedList(cursor.TableId);
                journalList.sortOrder(fieldNum(ProjInvoiceJour, InvoiceAccount), fieldNum(ProjInvoiceJour, LedgerVoucher));
                documentTypeMap.insert(documentType, journalList);
            }
            journalList.ins(cursor);
        }

        docTypeMapEnum = documentTypeMap.getEnumerator();
        while (docTypeMapEnum.moveNext())
        {
            journalList  = docTypeMapEnum.currentValue();

            controllerArgs = new Args();
            controllerArgs.object(journalList);
            controllerArgs.parm(DocConstant::PrintManagementPreview);     // We added this line to the original method

            MenuFunction::runCalled(_menuItemName, MenuItemType::Output, false, controllerArgs);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>PrintArchiveDownload_DC_Clicked_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the PrintArchiveDownload_DC button is clicked.
    /// </summary>
    /// <param name = "_control">The form control object. PrintArchiveDownload_DC control expected</param>
    public void PrintArchiveDownload_DC_Clicked_DC(FormControl _control)
    {
        DocPrintArchiveDownload::printArchiveDownload(this, DocJournalType::ProjInvoiceJour, ProjInvoiceJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>PrintArchive_DC_Clicked_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the PrintArchive_DC button is clicked.
    /// </summary>
    /// <param name = "_control">The form control object. PrintArchive_DC control expected</param>
    public void PrintArchive_DC_Clicked_DC(FormControl _control)
    {
        DocPrintArchiveHelper::refreshPrintArchiveViewMenuButton(
            this.design().controlName(formControlStr(ProjInvoiceJournal, PrintArchiveView_DC)), DocJournalType::ProjInvoiceJour, ProjInvoiceJour);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>