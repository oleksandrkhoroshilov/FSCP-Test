<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustQuotationConfirmationJournalForm_DC_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class CustQuotationConfirmationJournalForm_DC_Extension is an extension of the form CustQuotationConfirmationJournal.
/// </summary>
[ExtensionOf(formStr(CustQuotationConfirmationJournal))]
final class CustQuotationConfirmationJournalForm_DC_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the form.
    /// </summary>
    public void init()
    {
        next init();

        SalesQuotationConfirmationPrintMgmtPreview_DC.registerOverrideMethod(methodStr(FormMenuButtonControl, clicked), formMethodStr(CustQuotationConfirmationJournal, SalesQuotationConfirmationPrintMgmtPreview_DC_Clicked_DC), this);
        PrintArchiveDownload_DC.registerOverrideMethod(methodStr(FormMenuButtonControl, clicked), formMethodStr(CustQuotationConfirmationJournal, PrintArchiveDownload_DC_Clicked_DC), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>CustQuotationConfirmJour_OnActivated_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the form data source is activated.
    /// </summary>
    public void CustQuotationConfirmJour_OnActivated_DC()
    {
        this.refreshPrintArchiveView_DC();
    }

]]></Source>
			</Method>
			<Method>
				<Name>SalesQuotationConfirmation_OnLostFocus_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the SalesQuotationConfirmation menu button loses focus.
    /// </summary>
    public void SalesQuotationConfirmation_OnLostFocus_DC()
    {
        this.refreshPrintArchiveView_DC();
    }

]]></Source>
			</Method>
			<Method>
				<Name>SalesQuotationConfirmationPrintMgmtPreview_DC_Clicked_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the SalesQuotationConfirmationPrintMgmtPreview_DC menu item is clicked.
    /// </summary>
    /// <param name = "_control">A form control</param>
    public void SalesQuotationConfirmationPrintMgmtPreview_DC_Clicked_DC(FormControl _control)
    {
        if (isConfigurationkeyEnabled(configurationKeyNum(Project)))
        {
            this.launchReport_DC(PrintCopyOriginal::OriginalPrint);
        }
        else
        {
            Args args = new Args();
            args.caller(this);
            args.parm(DocConstant::PrintManagementPreview);

            new MenuFunction(menuItemOutputStr(SalesQuotationConfirmation), MenuItemType::Output).run(args);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>launchReport_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copied from CustQuotationConfirmationJournal.
    /// Everything is same, we only added 2 lines where we set reportArgs.parm().
    /// </summary>
    /// <param name = "_printCopyOriginal">Enum that specifies print management or
    /// original or copy printing. We pass the print management type and
    /// add our specific parm to force previewing.</param>
    private void launchReport_DC(PrintCopyOriginal _printCopyOriginal)
    {
        RecordSortedList            salesQuotationConfirmList = new RecordSortedList(tableNum(CustQuotationConfirmJour));
        RecordSortedList            projQuotationConfirmList  = new RecordSortedList(tableNum(CustQuotationConfirmJour));
        CustQuotationConfirmJour    cursor;
        str                         salesReportMenuItemName;
        str                         projReportMenuItemName;
        MenuFunction                reportMenuFunction;
        Args                        reportArgs;

        switch (_printCopyOriginal)
        {
            case PrintCopyOriginal::OriginalPrint:
                salesReportMenuItemName = menuitemOutputStr(SalesQuotationConfirmation);
                projReportMenuItemName  = menuitemOutputStr(PSAQuotationsConfirmPrint);
                break;

            case PrintCopyOriginal::Original:
                salesReportMenuItemName = menuitemOutputStr(SalesQuotationOriginalConfirmation);
                projReportMenuItemName  = menuitemOutputStr(PSAQuotationsOriginalConfirm);
                break;

            case PrintCopyOriginal::Copy:
                salesReportMenuItemName = menuitemOutputStr(SalesQuotationCopyConfirmation);
                projReportMenuItemName  = menuitemOutputStr(PSAQuotationsCopyConfirm);
                break;
        }

        for (cursor = getFirstSelection(custQuotationConfirmJour_ds);
        cursor;
        cursor = custQuotationConfirmJour_ds.getNext())
        {
            if (cursor.QuotationType == QuotationType::Project)
            {
                projQuotationConfirmList.ins(cursor);
            }
            else
            {
                salesQuotationConfirmList.ins(cursor);
            }
        }

        if (projQuotationConfirmList.len() > 0)
        {
            reportArgs = new Args();
            reportArgs.object(projQuotationConfirmList);
            reportArgs.parm(DocConstant::PrintManagementPreview);     // We added this line to the original method

            reportMenuFunction = new MenuFunction(projReportMenuItemName, MenuItemType::Output);
            reportMenuFunction.run(reportArgs);
        }

        if (salesQuotationConfirmList.len() > 0)
        {
            reportArgs = new Args();
            reportArgs.object(salesQuotationConfirmList);
            reportArgs.parm(DocConstant::PrintManagementPreview);     // We added this line to the original method

            reportMenuFunction = new MenuFunction(salesReportMenuItemName, MenuItemType::Output);
            reportMenuFunction.run(reportArgs);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>PrintArchiveDownload_DC_Clicked_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the PrintArchiveDownload_DC button is clicked.
    /// </summary>
    /// <param name = "_control">The form control object. PrintArchiveDownload_DC control expected</param>
    public void PrintArchiveDownload_DC_Clicked_DC(FormControl _control)
    {
        DocPrintArchiveDownload::printArchiveDownload(this, DocJournalType::CustQuotationConfirmJour, CustQuotationConfirmJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>refreshPrintArchiveView_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Used to toggle Print archive -> View button and to display the number of related documents in print archive.
    /// </summary>
    public void refreshPrintArchiveView_DC()
    {
        DocPrintArchiveHelper::refreshPrintArchiveViewMenuButton(
            this.design().controlName(formControlStr(CustQuotationConfirmationJournal, PrintArchiveView_DC)), DocJournalType::CustQuotationConfirmJour, CustQuotationConfirmJour);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>