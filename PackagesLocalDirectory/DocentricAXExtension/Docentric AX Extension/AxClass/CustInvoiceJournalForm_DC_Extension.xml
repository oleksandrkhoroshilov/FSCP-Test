<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustInvoiceJournalForm_DC_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class CustInvoiceJournalForm_DC_Extension is an extension of the form CustInvoiceJournal.
/// </summary>
[ExtensionOf(formStr(CustInvoiceJournal))]
final class CustInvoiceJournalForm_DC_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the form.
    /// </summary>
    public void init()
    {
        next init();

        SalesInvoicePrintMgmtPreview_DC.registerOverrideMethod(methodStr(FormMenuButtonControl, clicked), formMethodStr(CustInvoiceJournal, SalesInvoicePrintMgmtPreview_DC_Clicked_DC), this);
        PrintArchiveDownload_DC.registerOverrideMethod(methodStr(FormMenuButtonControl, clicked), formMethodStr(CustInvoiceJournal, PrintArchiveDownload_DC_Clicked_DC), this);
        PrintArchive_DC.registerOverrideMethod(methodStr(FormMenuButtonControl, clicked), formMethodStr(CustInvoiceJournal, PrintArchive_DC_Clicked_DC), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>SalesInvoicePrintMgmtPreview_DC_Clicked_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the SalesInvoicePrintMgmtPreview_DC menu item is clicked.
    /// </summary>
    /// <param name = "_control">A form control</param>
    public void SalesInvoicePrintMgmtPreview_DC_Clicked_DC(FormControl _control)
    {
        this.launchReport_DC();
    }

]]></Source>
			</Method>
			<Method>
				<Name>launchReport_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copied from CustInvoiceJournal, clicked() method for Print management print.
    /// Everything is same, we only added a line where we set args.parm().
    /// </summary>
    void launchReport_DC()
    {        
        CustInvoiceJour custInvoiceJourLocal;
        Args args = new Args();
        args.parm(DocConstant::PrintManagementPreview);     // We only added this line to the original method

        RecordSortedList journalList = SalesInvoiceJournalPrint::construct().newJournalList();
        ReprintOriginals_IS reprintOriginals;
        #ISOCountryRegionCodes

        reprintOriginals = SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIS]) ? ReprintOriginals_IS::Prompt : ReprintOriginals_IS::Always;

        //    args.caller(this); Does this have to be set?
        for (custInvoiceJourLocal = getFirstSelection(custInvoiceJour_ds); custInvoiceJourLocal; custInvoiceJourLocal = custInvoiceJour_ds.getNext())
        {
            if (reprintOriginals == ReprintOriginals_IS::Prompt &&
                    CustInvoiceJour::findRecId(custInvoiceJourLocal.RecId).PrintedOriginals &&
                    Box::yesNo(strFmt("@SYS125573",
                                      custInvoiceJourLocal.InvoiceId,
                                      custInvoiceJourLocal.InvoiceDate),
                               DialogButton::No) == DialogButton::No)
            {
                continue;
            }

            journalList.ins(custInvoiceJourLocal);
        }

        journalList.first(custInvoiceJourLocal);
        while (custInvoiceJourLocal)
        {
            args.record(custInvoiceJourLocal);

            if (custInvoiceJourLocal.SalesId)
            {
                // Sales order invoice
                new MenuFunction(menuitemOutputStr(SalesInvoice), MenuItemType::Output).run(args);
            }
            else
            {
                // Free text invoice
                new MenuFunction(menuitemOutputStr(FreeTextInvoice), MenuItemType::Output).run(args);
            }

            if (! journalList.next(custInvoiceJourLocal))
            {
                break;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>PrintArchiveDownload_DC_Clicked_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the PrintArchiveDownload_DC button is clicked.
    /// </summary>
    /// <param name = "_control">The form control object. PrintArchiveDownload_DC control expected</param>
    public void PrintArchiveDownload_DC_Clicked_DC(FormControl _control)
    {
        DocPrintArchiveDownload::printArchiveDownload(this, DocJournalType::CustInvoiceJour, CustInvoiceJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>PrintArchive_DC_Clicked_DC</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called when the PrintArchive_DC button is clicked.
    /// </summary>
    /// <param name = "_control">The form control object. PrintArchive_DC control expected</param>
    public void PrintArchive_DC_Clicked_DC(FormControl _control)
    {
        DocPrintArchiveHelper::refreshPrintArchiveViewMenuButton(
            this.design().controlName(formControlStr(CustInvoiceJournal, PrintArchiveView_DC)), DocJournalType::CustInvoiceJour, CustInvoiceJour);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>