<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchPackingSlipJournalPost_DC_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Docentric.AX.Reporting;

/// <summary>
/// The class PurchPackingSlipJournalPost_DC_Extension is an extension of the class PurchPackingSlipJournalPost.
/// </summary>
[ExtensionOf(classStr(PurchPackingSlipJournalPost))]
final class PurchPackingSlipJournalPost_DC_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createReportData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates report data.
    /// </summary>
    /// <param name = "_formletterJournalPrint">An instance of the FormletterJournalPrint class</param>
    /// <returns>True if the report data was successfully created; otherwise false</returns>
    public boolean createReportData(FormletterJournalPrint _formletterJournalPrint)
    {
        boolean res = next createReportData(_formletterJournalPrint);

        VendPackingSlipJour journal;

        try
        {
            if (this.isProforma())
            {
                // Get the journal record.
                journal = this.parmJournalTable();

                // Get the journal version record.
                VendPackingSlipVersion journalVersion = VendPackingSlipVersion::findLatest(journal.RecId);
                if (journalVersion.RecId)
                {
                    // Get the journal lines and journal version lines.
                    Map journalLineMap = new Map(Types::Int64, Types::Record);
                    Map journalVersionLineMap = new Map(Types::Int64, Types::Record);

                    VendPackingSlipTrans vpst;
                    while select vpst
                        where vpst.VendPackingSlipJour == journal.RecId
                    {
                        journalLineMap.insert(vpst.RecId, vpst);
                        VendPackingSlipTransHistory vpstHistory = VendPackingSlipTransHistory::find(vpst.RecId);
                        if (vpstHistory)
                        {
                            journalVersionLineMap.insert(vpst.RecId, vpstHistory);
                        }
                    }
    
                    DocReportProformaPackingSlipDataSource proformaDsObject = new DocReportProformaPackingSlipDataSource();
                    proformaDsObject.parmJournal(journal);
                    proformaDsObject.parmJournalLines(journalLineMap);
                    proformaDsObject.parmPackingSlipVersion(journalVersion);
                    proformaDsObject.parmPackingSlipTransHistoryLines(journalVersionLineMap);

                    // Add the proformaDsObject to the Docentric's report temporary storage.
                    TemporaryReportDataStorage::AddDataObject(DocConstantDSP::Prefix_PurchPackingSlip, journal.RecId, proformaDsObject);
                }
            }
        }
        catch (Exception::CLRError)
        {
            str clrErrorMsg = DocGlobalHelper::getLastClrExceptionMessage();
    
            setPrefix(DocConstant::Docentric);
            error(clrErrorMsg);
    
            DocGlobalHelper::handleException(funcName(), 
                strFmt("@DocentricAX:FailedToStoreProformaPackingSlip", journal.PurchId), DocConstantDiagnostics::ErrProformaReportTempStorage);
        }
        catch
        {
            DocGlobalHelper::handleException(funcName(), 
                strFmt("@DocentricAX:FailedToStoreProformaPackingSlip", journal.PurchId), DocConstantDiagnostics::ErrProformaReportTempStorage);
        }

        return res;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>