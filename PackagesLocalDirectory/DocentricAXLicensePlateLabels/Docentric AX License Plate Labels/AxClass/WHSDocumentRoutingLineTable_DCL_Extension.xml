<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSDocumentRoutingLineTable_DCL_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class WHSDocumentRoutingLineTable_DCL_Extension is an extension of the table WHSDocumentRoutingLine.
/// </summary>
[ExtensionOf(tableStr(WHSDocumentRoutingLine))]
final class WHSDocumentRoutingLineTable_DCL_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the field values for a new record.
    /// </summary>
    public void initValue()
    {
        next initValue();

        this.PackedQueryRun_DCL = this.defaultPackedQueryRun_DCL().pack();
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultPackedQueryRun_DCL</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the default query run.
    /// </summary>
    /// <returns>The default query run.</returns>
    [Hookable(false)]
    internal QueryRun defaultPackedQueryRun_DCL()
    {
        return new QueryRun(DocConstantLicensePlateLabel::DefaultWHSLicensePlateLabelQuery);
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryCriteriaConfigured_DCL</Name>
				<Source><![CDATA[
    /// <summary>
    /// A display method indicating if the default query criteria has been changed.
    /// </summary>
    /// <returns>True/False</returns>
    public display DocWHSQueryCriteriaConfigured queryCriteriaConfigured_DCL()
    {
        if (this.PackedQueryRun_DCL == conNull())
        {
            return false;
        }

        QueryRun qrCurrent = new QueryRun(this.PackedQueryRun_DCL);

        return DocGlobalHelper::getQueryCriteria(qrCurrent.query(), true, true) != DocGlobalHelper::getQueryCriteria(this.defaultPackedQueryRun_DCL().query(), true, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryCriteria_DCL</Name>
				<Source><![CDATA[
    /// <summary>
    /// A display method for defined query criteria.
    /// </summary>
    /// <returns>Defined query criteria as string</returns>
    public display DocWHSQueryCriteria queryCriteria_DCL()
    {
        if (this.PackedQueryRun_DCL == conNull())
        {
            return '';
        }

        QueryRun qrCurrent = new QueryRun(this.PackedQueryRun_DCL);

        return DocGlobalHelper::getQueryCriteria(qrCurrent.query(), true, true);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>